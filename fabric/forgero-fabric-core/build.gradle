dependencies {
	api project(':forgero-core')
	api project(path: ':fabric:modules:generator', configuration: 'namedElements')
	api project(path: ':fabric:minecraft-common', configuration: 'namedElements')

	modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modApi "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation("maven.modrinth:arrp:${project.aarp_version}")
	include("maven.modrinth:arrp:${project.aarp_version}")

	localRuntime project(':content:forgero-vanilla')
	localRuntime project(':content:forgero-extended')
	localRuntime project(':content:forgero-compat')
	localRuntime project(':content:forgero-structures')

	localRuntime project(path: ':fabric:modules:bows', configuration: 'namedElements')
}

loom {
	accessWidenerPath = file("src/main/resources/forgero-fabric-core.accesswidener")

	runs {
		testmodServer {
			server()
			ideConfigGenerated project.rootProject == project
			name = "Testmod Server"
			source sourceSets.test
		}
		gametest {
			inherit testmodServer

			name "Game Test"
			// Enable the gametest runner
			vmArg "-Dfabric-api.gametest"
			vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/junit.xml"
			runDir "build/gametest"
		}
	}
}

sourceSets {
	test {
		compileClasspath += test.compileClasspath
		runtimeClasspath += test.runtimeClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

test {
	useJUnitPlatform()
	maxParallelForks = 32
	testLogging.events("failed")
	testLogging.info.events = ["failed", "skipped"]
}
