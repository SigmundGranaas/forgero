//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
//file:noinspection DependencyNotationArgument

dependencies {
	// Fabric common module
	api(project(path: ":fabric:minecraft-common", configuration: 'namedElements'))

	// Fabric generator module
	api(project(path: ":fabric:modules:generator", configuration: 'namedElements'))

	// Bow and arrows
	// Arrows and bow tests are still in the core fabric module. This dependency is needed until they are moved to their own module.
	// Ref: https://github.com/SigmundGranaas/forgero/issues/933
	localRuntime(project(path: ":fabric:modules:bows", configuration: 'namedElements'))

	localRuntime(project(path: ":fabric:modules:tridents", configuration: 'namedElements'))


	// Trade
	localRuntime(project(path: ":fabric:modules:trade", configuration: 'namedElements'))

	// Fabric Loader
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// Fabric API
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}")

	// Advanced Runtime Resource Pack
	modApi("maven.modrinth:arrp:${project.advanced_runtime_resource_pack_version}")

	modApi("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}"){
		exclude(group: "net.fabricmc.fabric-loader")
	}
}

sourceSets {
	test {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

loom {
	accessWidenerPath = file("src/main/resources/${mod_id}-core.accesswidener")

	runs {
		testServer {
			server()
			ideConfigGenerated project.rootProject == project
			name = "Test Server"
			source sourceSets.test
		}
		gameTest {
			inherit testServer
			name "Game Test"
			// Enable the gametest runner
			vmArg "-Dfabric-api.gametest"
			runDir "build/gameTest"
		}
		testClient {
			client()
			name = "Test Client"
			vmArg "-Dfabric-api.gametest.structures.output-dir=${file("src/test/resources/data/${mod_id}/gametest/structures")}"

			ideConfigGenerated = false
			source sourceSets.test
		}
	}
}

test {
	useJUnitPlatform()
	maxParallelForks = 32
	testLogging.events("failed")
	testLogging.info.events = ["failed", "skipped"]
}
