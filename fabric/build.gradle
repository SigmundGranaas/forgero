plugins {
	id "fabric-loom" version '1.5-SNAPSHOT' apply true
	id "com.modrinth.minotaur" version "2.+"
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

configure(allprojects) {
	apply plugin: "idea"
	apply plugin: 'fabric-loom'
}

archivesBaseName = "forgero-fabric"

dependencies {
	minecraft "com.mojang:minecraft:$project.minecraft_version"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	api project(path: ':fabric:forgero-fabric-core', configuration: 'namedElements')

	include project(':forgero-core')

	include project(path: ':fabric:forgero-fabric-core', configuration: 'namedElements')
	include project(path: ':fabric:minecraft-common', configuration: 'namedElements')
	include project(path: ':fabric:forgero-fabric-compat', configuration: 'namedElements')
	include project(path: ':fabric:modules:generator', configuration: 'namedElements')

	include project(':content:forgero-vanilla')
	include project(':content:forgero-extended')
	include project(':content:forgero-compat')
	include project(':content:forgero-structures')
	include project(':fabric:modules:generator')
	include project(':fabric:modules:bows')

	api project(path: ':fabric:forgero-fabric-core', configuration: 'namedElements')

	runtimeOnly project(':content:forgero-vanilla')
	runtimeOnly project(':content:forgero-extended')
	runtimeOnly project(':content:forgero-compat')
	runtimeOnly project(':content:forgero-structures')
}

subprojects {
	dependencies {
		minecraft "com.mojang:minecraft:$project.minecraft_version"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modCompileOnly "net.fabricmc:fabric-loader:${project.loader_version}"

		annotationProcessor 'org.projectlombok:lombok:1.18.32'
		implementation 'org.projectlombok:lombok:1.18.32'

		implementation 'org.jetbrains:annotations:23.0.0'
		testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
	}

	test {
		useJUnitPlatform()
	}

	java {
		withSourcesJar()
	}

}

curseforge {
	apiKey = System.getenv("CURSEFORGE_API_KEY") ?: ""
	project {
		id = '581435'
		changelogType = "markdown"
		changelog = file('../changelog.md')

		releaseType = project.release_type
		addGameVersion project.minecraft_version
		addGameVersion 'Fabric'
		addGameVersion 'Quilt'
		addGameVersion "Java 17"

		mainArtifact(remapJar) {
			displayName = "Forgero [Fabric]: $version"
			relations {
				embeddedLibrary 'arrp'
				requiredDependency 'fabric-api'
				optionalDependency 'patchouli'
				optionalDependency "lambdynamiclights"
				optionalDependency "better-combat"
				optionalDependency "modmenu"
				optionalDependency "mythicmetals"
				optionalDependency "betternether"
				optionalDependency "mystical-crops"
				optionalDependency "mythicmetals"
				incompatible "exordium"
			}
		}
	}
	options {
		forgeGradleIntegration = false // defaults to true
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")

	projectId = project.modid
	versionNumber = version
	versionType = project.release_type
	uploadFile = remapJar
	gameVersions = [minecraft_version]
	loaders = ["fabric", "quilt"]
	versionName = "Forgero $version"
	changelog = file('../changelog.md').text
	dependencies {
		embedded.project "arrp"
		required.project "fabric-api"
		optional.project "patchouli"
		optional.project "lambdynamiclights"
		optional.project "better-combat"
		optional.project "modmenu"
		optional.project "mythicmetals"
		optional.project "betternether"
		optional.project "mystical_crops"
		optional.project "mythicmetals"
		optional.project "wiredredstone"
		incompatible.project "exordium"
	}
}

task publishToModrinthAndCurseForge {
	dependsOn 'modrinth', 'curseforge'
}

allprojects {
	processResources {
		inputs.property "version", rootProject.version

		filesMatching("fabric.mod.json") {
			expand "version": rootProject.version
		}
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${archivesBaseName}" }
		}
	}
	java {
		withSourcesJar()
	}
}
