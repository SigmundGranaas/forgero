//file:noinspection GroovyAssignabilityCheck

plugins {
	id "idea"
	id "fabric-loom" version "1.1.8" apply true
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'maven-publish'
}

configure(subprojects) {
	apply plugin: 'java-library'
	apply plugin: "idea"
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'
}

archivesBaseName = "forgero-fabric"

dependencies {
	minecraft "com.mojang:minecraft:$project.minecraft_version"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	include "net.devtech:arrp:0.6.7"
	include project(':forgero-core')

	include project(':fabric:forgero-fabric-core')
	include project(':fabric:minecraft-common')
	include project(':fabric:forgero-fabric-compat')

	include project(':content:forgero-vanilla')
	include project(':content:forgero-extended')
	include project(':content:forgero-compat')
	include project(':content:forgero-structures')

	include "net.devtech:arrp:0.6.7"
}

subprojects {
	dependencies {
		minecraft "com.mojang:minecraft:$project.minecraft_version"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

		annotationProcessor 'org.projectlombok:lombok:1.18.24'

		implementation 'org.jetbrains:annotations:23.0.0'
		implementation 'org.projectlombok:lombok:1.18.24'
		testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
	}
	// configure the maven publication
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				// add all the jars that should be included when publishing to maven

			}
		}

		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
		repositories {
			maven {
				name = "GitHubPackages"
				url = "https://maven.pkg.github.com/sigmundgranaas/forgero"
				credentials {
					username = System.getenv("GITHUB_ACTOR")
					password = System.getenv("GITHUB_TOKEN")
				}
			}
			// Add repositories to publish to here.
			// Notice: This block does NOT have the same function as the block in the top level.
			// The repositories here will be used for publishing your artifact, not for
			// retrieving dependencies.
		}
	}
}

curseforge {
	apiKey = System.getenv("CurseForgeApiKey") ?: ""
	project {
		id = '581435'
		changelogType = "markdown"
		changelog = file('changelog.md')

		releaseType = 'release'
		addGameVersion project.minecraft_version
		addGameVersion 'Fabric'
		addGameVersion 'Quilt'
		addGameVersion "Java 17"

		mainArtifact(remapJar) {
			displayName = "Forgero [Fabric]: $rootProject.version"
			relations {
				embeddedLibrary 'arrp'
				requiredDependency 'fabric-api'
				optionalDependency 'patchouli'
			}
		}
	}
	options {
		forgeGradleIntegration = false // defaults to true
	}
}

allprojects {
	repositories {
		maven { url "https://api.modrinth.com/maven" }
		maven { url "https://cursemaven.com" }
		maven { url 'https://jitpack.io' }
		maven {
			url = uri("https://ueaj.dev/maven")
		}
	}

	processResources {
		inputs.property "version", rootProject.version

		filesMatching("fabric.mod.json") {
			expand "version": rootProject.version
		}
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${archivesBaseName}" }
		}
	}
}
