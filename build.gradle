plugins {
	id "maven-publish"
}
allprojects {
	repositories {
		mavenCentral()
		maven { url "https://api.modrinth.com/maven" }
		maven { url "https://cursemaven.com" }
		maven { url 'https://jitpack.io' }
		maven { url "https://maven.shedaniel.me/" }
		maven { url 'https://maven.blamejared.com' }
		maven { url "https://minecraft.guntram.de/maven/" }
		maven { url "https://maven.terraformersmc.com/" }
		maven { url "https://maven.wispforest.io/" }
		maven { url "https://maven.cafeteria.dev" }
		maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
		maven { url 'https://maven.gegy.dev' }
		maven { url 'https://maven.kosmx.dev/' }
		maven { url 'https://jitpack.io' }
		maven {
			name = "Xander Maven"
			url = "https://maven.isxander.dev/releases"
		}
		maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }

		mavenLocal()
	}
}


def isSnapshotVersion = { versionString ->
	return versionString.contains("-g")
}

def getGitVersion = { ->
	def projectVersion = project.forgero_version + "+" + project.minecraft_version
	try {
		def stdout = new ByteArrayOutputStream()
		exec {
			commandLine "git", "describe", "--tags"
			standardOutput = stdout
		}

		def gitVersionString = stdout.toString().trim()
		def gitVersionStringParsed = gitVersionString.startsWith("v") ? gitVersionString.substring(1) : gitVersionString

		// If it's a snapshot version, append '-SNAPSHOT'
		if (isSnapshotVersion(gitVersionStringParsed)) {
			return gitVersionStringParsed += "-SNAPSHOT"
		} else {
			return projectVersion
		}
	}
	catch (Exception ignored) {
		return projectVersion
	}
}

println("Forgero version: ${getGitVersion()}")

version = "${getGitVersion()}"
group = project.maven_group

subprojects {
	version = "${getGitVersion()}"
	group = project.maven_group

	repositories {
		mavenCentral()
		mavenLocal()
	}
}

subprojects { project ->
	if (project.name != 'content') {
		apply plugin: "maven-publish"
		apply plugin: "java-library"
		repositories {
			mavenCentral()
			mavenLocal()
		}

		publishing {
			repositories {
				maven {
					name = "ArchivaMavenRepo"
					// Determine if it's a snapshot and adjust the URL accordingly.
					url = isSnapshotVersion(project.version) ?
						"https://maven.sigmundgranaas.com/repository/snapshots/" :
						"https://maven.sigmundgranaas.com/repository/internal/"
					credentials {
						username 'admin'
						password System.getenv("ARCHIVA_ADMIN_KEY")
					}
				}
			}

			publications {
				mavenJava(MavenPublication) {
					groupId = group
					if (project.name === 'fabric') {
						artifactId = 'forgero-fabric'
						archivesBaseName = 'forgero-fabric'
					} else {
						artifactId = archivesBaseName
					}
					from components.java
				}
			}

		}
	}
}


task copyJars(type: Copy) {
	group = 'build'
	from subprojects.collect { it.tasks.withType(AbstractArchiveTask) }
	exclude "**/*-dev.jar"
	into "$buildDir/libs"
}
