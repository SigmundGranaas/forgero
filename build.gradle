//file:noinspection GroovyAssignabilityCheck

plugins {
	id "maven-publish"
}

def getGitVersion = { ->
	def projectVersion = project.forgero_version + "+" + project.minecraft_version
	try {
		def stdout = new ByteArrayOutputStream()
		exec {
			commandLine "git", "describe", "--tags"
			standardOutput = stdout
		}

		def gitVersionString = stdout.toString().trim()
		def gitVersionStringParsed = gitVersionString.startsWith("v") ? gitVersionString.substring(1) : gitVersionString

		def parts = gitVersionStringParsed.split("-")

		if (parts.length == 2 && parts[1] == project.minecraft_version) {
			return gitVersionStringParsed
		} else {
			return projectVersion
		}
	}
	catch (Exception ignored) {
		return projectVersion
	}
}

println("Forgero version: ${getGitVersion()}")

version = "${getGitVersion()}"
group = project.maven_group

subprojects {
	version = "${getGitVersion()}"
	group = project.maven_group

	repositories {
		mavenCentral()
		mavenLocal()
	}
}

subprojects {
	if (it.name != 'content') {
		apply plugin: "maven-publish"
		apply plugin: "java-library"
		repositories {
			mavenCentral()
			mavenLocal()
		}

		publishing {
			repositories {
				maven {
					name = "GitHubPackages"
					url = uri("https://maven.pkg.github.com/sigmundgranaas/forgero")
					credentials {
						username = "sigmundgranaas"
						password = System.getenv("GPR_API_KEY")
					}
				}
			}


			publishing {
				publications {
					gpr(MavenPublication) {
						groupId = group // Replace with your groupId
						artifactId = archivesBaseName // Replace with your module's name
						from components.java
					}
				}
			}
		}
	}
}


task copyJars(type: Copy) {
	group = 'build'
	from subprojects.collect { it.tasks.withType(AbstractArchiveTask) }
	exclude "**/*-dev.jar"
	into "$buildDir/libs"
}
